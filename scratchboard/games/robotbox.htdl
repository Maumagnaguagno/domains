task CarryThruDoor(box, door, room1, room2):
    precond:
        Box(box) and Door(door) and Room(room1) and Room(room2)
        Connects(door, room1, room2)
        AtRoom(box, room1) and Open(door) and Loaded(box)
    add:
        AtRoom(box, room2)
    delete:
        AtRoom(box, room1)
    cost:
        CarryThruDoorCost

task PullThruDoor(box, door, room1, room2):
    precond:
        Box(box) and Door(door) and Room(room1) and Room(room2)
        Connects(door, room1, room2)
        AtRoom(box, room1) and Open(door) and Attached(box)
    add:
        AtRoom(box, room2)
    delete:
        AtRoom(box, room1)
    cost:
        PullThruDoorCost

task OpenDoor(door):
    precond:
        Door(door) and Openable(door) and not Open(door)
    add:
        Open(door)
    cost:
        OpenDoorCost

task AttachBox(box):
    precond:
        Box(box) and not Attached(box)
    add:
        Attached(box)
    cost:
        AttachBoxCost

task LoadBox(box):
    precond:
        Box(box) and not Loaded(box)
    add:
        Loaded(box)
    cost:
        LoadBoxCost

task AchieveAttached(box):
    precond:
        Box(box)
    add:
        Attached(box)
    method:
        precond:
            Attached(box)
    method:
        precond:
            not Attached(box) and not Loaded(box)
        subtasks:
            AttachBox(box)
    optimistic:
        0
    pessimistic:
        AttachBoxCost + Epsilon
    level:
        1

task AchieveLoaded(box):
    precond:
        Box(box)
    add:
        Loaded(box)
    method:
        precond:
            Loaded(box)
    method:
        precond:
            not Attached(box) and not Loaded(box)
        subtasks:
            LoadBox(box)
    optimistic:
        0
    pessimistic:
        LoadBoxCost + Epsilon
    level:
        1

task MoveThruDoor(box, door, room1, room2):
    precond:
        Box(box) and Door(door) and Room(room1) and Room(room2)
        Connects(door, room1, room2)
        AtRoom(box, room1) and Open(door)
    add:
        AtRoom(box, room2)
    delete:
        AtRoom(box, room1)
    abstract:
        Attached(box)
        Loaded(box)
    method:
        subtasks:
            n1 : AchieveLoaded(box)
            n2 : CarryThruDoor(box, door, room1, room2)
        order:
            n1 < n2
    method:
        subtasks:
            n1 : AchieveAttached(box)
            n2 : PullThruDoor(box, door, room1, room2)
        order:
            n1 < n2
    optimistic:
        Min(PullThruDoorCost, CarryThruDoorCost)
    pessimistic:
        Max(
            AttachBoxCost + Epsilon + PullThruDoorCost,
            LoadBoxCost + Epsilon + CarryThruDoorCost
        ) + Epsilon
    level:
        2

task AchieveOpen(door):
    precond:
        Door(door) and Openable(door)
    add:
        Open(door)
    method:
        precond:
            Open(door)
    method:
        precond:
            not Open(door)
        subtasks:
            OpenDoor(door)
    optimistic:
        0
    pessimistic:
        OpenDoorCost + Epsilon
    level:
        3

task AchieveAtRoom(box, room, depth):
    precond:
        Box(box) and Room(room)
    add:
        AtRoom(box, room)
    delete:
        AtRoom(box, room) forall room' \
            if Room(room') and room' != room and AtRoom(box, room')
    abstract:
        Attached(box)
        Loaded(box)
        Open(door) forall door if Openable(door) and not Open(door)
    method:
        precond:
            AtRoom(box, room)
    method door, room1, room2:
        precond:
            Connects(door, room1, room2)
            AtRoom(box, room1) and room1 != room
            Openable(door)
            depth < MaxDistance
        subtasks:
            n1 : AchieveOpen(door)
            n2 : MoveThruDoor(box, door, room1, room2)
            n3 : AchieveAtRoom(box, room, depth + 1)
        order:
            n1 < n2
            n2 < n3
    optimistic:
        0
    pessimistic:
        (MaxDistance - depth) * (
            OpenDoorCost + Epsilon +
            Max(
                AttachBoxCost + Epsilon + PullThruDoorCost,
                LoadBoxCost + Epsilon + CarryThruDoorCost) + Epsilon +
            Epsilon)
    level:
        4
